/* Gram√°tica OMI en EBNF */   
program ::= stmts
   | empty
   
stmts ::= stmt [;]?
   |  stmt ";" stmts
   |  stmtb 
   |  label stmts
   |  ";"
   
stmtb ::= define
   |  if
   |  with
   |  while
   |  dowhile
   |  for
   |  foreach
   |  switch
   |  iloop
   |  trycatch
   
stmt ::= exp
   | "<<" exp
   | ">>" ("[" exp "]")? id
   | "goto" exp
   | "include" exp
   | "return" exp?
   | "sleep" exp 
   | "load" exp 
   | "typeof" id 
   | "datinfo" exp
   | "exit"
   | "throw" exp
   | "global" identity
   | "break" num? ";"
   | "continue" num? ";"
   
label ::= id ":"

define ::= function
   | class
 /*  | component */
   
if ::= "if" exp ( "{" stmts? "}" | stmt ";" | stmtb ) ("else" ( "{" stmts? "}" | stmt ";" | stmtb ) )?

with ::= "with" exp ( "{" stmts? "}" | stmt ";" | stmtb )
   
while ::= "while" exp ( "{" stmts? "}" | stmt ";" | stmtb )


dowhile ::= "do" ( "{" stmts? "}" | stmt ";" | stmtb ) "while" exp ";"

for ::= "for" "("? exp? ";" exp? ";" exp? ")"? ( "{" stmts? "}" | stmt ";" | stmtb )

   
foreach ::= "for" "("? ( id (":" id)? "in" exp | exp "as" id (":" id)? ) ")"? ( "{" stmts? "}" | stmt ";" | stmtb )


switch ::= "switch" "(" exp ")" "{" cases? "}"

cases ::= "case" exp ":" ( stmts cases | stmts | cases )
   |  "default" ":" stmts
   
iloop ::= "$" "(" exp ( "as" id (":" id)? )? ("," exp )? ")" ( "{" stmts? "}" | stmt ";" | stmtb )

trycatch ::= "try" ( "{" stmts? "}" | stmt ";" | stmtb ) catch "(" id ")" ( "{" stmts? "}" | stmt ";" |  stmtb )
   
function ::= "~" id "(" params_val? ")" "{" stmts? "}"

decorator ::= "~~" id "(" params_val? ")" "{" stmts? "}"

class ::= "class" id ("extends" id )? "{" class_stmts? "}"

class_stmts ::= 
   |  "static" function  class_stmts?
   |  "private" function class_stmts?
   |  "private" "static" function class_stmts?
   |  stmts

params_val ::= params_val "," id "=" identity 
   |  params "," id "=" identity 
   |  id "=" identity 
   |  params
   
params ::= params "," "&"? id
   |  "&"? id

exp ::= op1


op1 ::= op1 ( "||" | "or" ) op2
   |  op2

op2 ::= op2 ( "&&" | "and" ) op3
   |  op3

op3 ::= "!" op3 
   |  op4

op4 ::= op4 "<" op5
   |  op4 "<=" op5
   |  op4 ">" op5
   |  op4 ">=" op5
   |  op4 "==" op5
   |  op4 "!=" op5
   |  op4 "===" op5
   |  op4 "!==" op5
   |  op5

op5 ::= op5 "+" op6
   |  op5  "-" op6
   |  op6
   
op6  ::= op6 "*" op7
   |  op6 "/" op7
   |  op7
   
op7  ::= op7 "^" op8
   |  op7 "%" op8
   |  op8

op8 ::=  op8 "." op9
   |  op9

op9  ::= call "<<" op9
   |  call
   
call ::= call "(" list? ")"
   |  opc
   
opc ::= tern
   |  nullcoalescing
   |  unity
   
tern ::= exp "?" exp? ":" exp
   |  exp "?" exp
   
nullcoalescing ::= "[[" list "]]"

unity ::= inc_dec
   |  assignation_exp
   |  cast
   |  logical_func
   |  arith_func
   |  array_func
   |  string_func
   |  regexp_func
   |  iloop_access
   |  class_exp
   |  func_exp
   |  file
   |  date
   |  process
   |  generator
   |  environments
   |  array
   |  identity
   
inc_dec ::= "++" exp
   |  exp "++"
   |  "--" exp
   |  exp "--"
   
assignation_exp ::= id_assig "+=" exp
   |  id_assig "-=" exp
   |  id_assig "*=" exp
   |  id_assig "/=" exp
   |  id_assig "^=" exp
   |  id_assig "%=" exp
   |  id_assig ".=" exp
   |  assignation
   
id_assig ::= id
   | assignation
   
cast ::= "int" exp
   | "float" exp
   |  "bool" exp
   |  "str" exp

logical_func ::= "isnull" exp
   |  "empty" exp
   
arith_func ::= "size" exp
   |  "sizeof" exp
   
string_func ::= "sprintf" "(" exp "," exp ")"
   |  "str_replace" "(" exp "," exp "," exp ("," exp)? ")"
   |  "str_subreplace" "(" exp "," exp "," exp "," exp ")"
   |  "str_find" "(" exp "," exp ("," exp)? ")"
   |  "str_upper" "(" exp ")"
   |  "str_lower" "(" exp ")"
   
array_func ::= "array_explode" "(" exp "," exp ")"
   |  "array_implode" "(" exp "," exp ")"
   |  "array_chunck" "(" exp "," exp ")"
   |  "array_reduce" "(" exp "," exp ")"
   
class_exp ::= "new" id ("(" list? ")")?
   |  "this" 
   |  "parent"
   |  namespace
   
array ::= "{" list "}"
   |  "{" map "}"
   |  "{""}"
   |  "arrayop"
   |  access

date ::= "date" "(" exp ")"
   |  "time" ("(" ")")

file ::= "file" "(" exp ("," exp)? ")"
   |  "fput" "(" exp "," exp ")"
   |  "fwrite" "(" exp "," exp ")"
   |  "fappend" "(" exp "," exp ")"
   |  "fget" "(" exp ("," exp)? ")"
   |  "fread" "(" exp ")"
   |  "fclose" "(" exp ")"
   |  "fseek" "(" exp "," exp ("," fposs)? ")"
   |  "ftell" "(" exp ")"

fpos ::= FSET
   |  FCUR
   | FEND
   
process ::= "exec" "(" exp ")"
   |  "eval" "(" exp ")"
   |  "fork" "(" ")"
   |  "wait" "(" exp? ")"
   |  "signal" "(" exp "," exp ")"
   |  "signalhandler" "(" exp "," exp ")"
   |  "exitProcess" "(" ")"
   |  "getpid" "(" ")"
   |  "getppid" "(" ")"
   |  "process" "(" exp ("," list)? ")"
   
environment ::= "getenv" "(" exp ")"

generator ::= "(" exp (":" exp)? "for" id (":" id )? "in" exp ( "if" exp )? ("{" stmts "}")? ")"
   | "(" exp (":" exp)? "for" "(" id (":" id )? "in" exp ")" ( "if" exp )? ("{" stmts "}")? ")"

regexp_func ::= "regexp" "(" exp ")"
   |  "search" "(" exp "," exp ("," list)? ")"
   |  "match" "(" exp "," exp ")"
   
iloop_access ::= "$"
   |  "$" "{" NUM "}"

identity ::=  num
   |  "true"
   |  "false"
   |  "null"
   |  str
   |  rexp
   |  id 
   
function_exp ::= function_lambda
   |  function_decorator 
   |  function_partial
   |  function_context
   
function_lambda ::= "~" "(" params_val? ")" "{" stmts? "}"
    | "~" params_val ":" exp
    | "~&" id

function_decorator ::= "~~" "(" params_val? ")" "{" stmts? "}"

function_partial ::= "P" "[" params_val "]" "(" id ")"
   |  "P" "[" params_val "]" "(" function_exp ")"
   |  "P" "[" params_val "]" "(" arrayexp ")"
   
function_context ::= "~>"

list ::= list "," exp?
   |  exp

map ::= map "," pair?
   |  pair
   
pair ::= exp ":" exp

assignation ::= id_assig "=" "&"? exp
   |  id_assig "=" function
   
access ::= access "->" id
   |  access "[" exp? "]"
   |  call "->" id
   |  call "[" exp? "]"

namespace ::= namespace "::" id
   |  id "::" id
   |  "parent" "::" id
   |  "static" "::" id
   
id ::= [a-zA-Z_][a-zA-Z0-9_]*

comments ::= "/*" [^*]* "*/"
   |  "//"[^\n]
   | "#" [^#]
   
num ::= "+" [0-9]+(.[0-9]+)?
   |  "-" [0-9]+(.[0-9]+)?

str ::= "'".* "'"

regexp ::= "`".*"`" 
	
